plugins {
	id 'java'
    id 'org.spongepowered.plugin' version '0.9.0'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'maven-publish'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/pietelite/griefalert")
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

repositories {
	mavenCentral()
    google()
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url "https://jitpack.io" }
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

archivesBaseName = 'griefalert' // Project Base Name Here
group = 'com.minecraftonline.sponge' // This is a MinecraftOnline project
version = '1.4.1' // Project Version

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

ext.sponge_version = '7.2.0' // SpongeAPI version
ext.authors = ['PietElite', 'darkdiplomat', 'BastetFurry', '14mRh4X0r']

compileJava {
    options.compilerArgs += ["-parameters"]
}

dependencies {
    implementation 'org.spongepowered:spongeapi:7.2.0'
    implementation 'com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT'
    implementation 'com.sk89q.worldedit:worldedit-sponge:6.1.7-SNAPSHOT'
    implementation fileTree(dir: 'libs', include: '*.jar')
//    shadow fileTree(dir: 'libs', include: 'shadow/*.jar')
    implementation 'com.github.randombyte-developer:holograms:v3.2.0'
    implementation 'org.xerial:sqlite-jdbc:3.8.11.2'
    implementation 'mysql:mysql-connector-java:8.0.19'
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.5.4'
    testImplementation('junit:junit:4.13')
}

sponge.plugin.meta {
    authors = ext.authors
}

jar {
//    classifier = "git describe --always".execute().text.trim()
}

shadowJar {
    configurations = [project.configurations.shadow]
    baseName = 'griefalert'
    classifier = ''
}

subprojects {
    apply plugin: 'java'
}

task apiJar(type: org.gradle.jvm.tasks.Jar) {
    baseName = 'griefalert-api'
    version = '0.0'
    classifier = "git describe --always".execute().text.trim()
    from(sourceSets.main.output) {
        include 'com/minecraftonline/griefalert/api/**'
    }
}

jar.manifest {
    attributes "Implementation-Title": name,
            "Implementation-Version": version,
            "Implementation-Vendor": "MinecraftOnline"
}

